# smittytone's zsh profile (Pi)
#
# 1.0.3

# Options
setopt AUTO_CD
setopt CORRECT


# History
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000


# Autocompletion
autoload -Uz compinit && compinit


# Prompts...
# ...left-hand side
PROMPT='%F{red}%K{0}%d%k%f > '

# ...right-hand side: use for Git repos
# FROM 1.0.2 - use a git check to show status as well as repo namw
git_prompt() {
    local ref
    is_dirty() {
        test -n "$(git status --porcelain --ignore-submodules)"
    }
    ref="$vcs_info_msg_0_"
    if [[ -n "$ref" ]]; then
        ref="%F{cyan}[${ref}]%f"
        if is_dirty; then
            ref="%F{yellow}%B!%b%f ${ref}"
        else
            ref="%F{green}%B#%b%f ${ref}"
        fi
        RPROMPT=$ref
    else
        RPROMPT=""
    fi
}

autoload -Uz add-zsh-hook
autoload -Uz vcs_info
precmd_vcs_info() {
    vcs_info
    git_prompt
}
add-zsh-hook precmd precmd_vcs_info
setopt prompt_subst
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes false
zstyle ':vcs_info:git*' formats '%b'
zstyle ':vcs_info:git*' actionformats '%F{cyan}[%b (%a)]%f'


# Aliases...
# ...list operations
alias ls='ls -l --group-directories-first --color=auto'
alias la='LC_COLLATE=C ls -lah --group-directories-first --color=auto'

# ...shutdown and restart
alias sd='sudo shutdown -h now'
alias rs='sudo shutdown -r now'

# ...tools
alias python='python3'


# List operation colour scheme
LS_COLORS=$LS_COLORS:'di=0;36:ln=0;93:ex=0;35:' ; export LS_COLORS


# Functions...
# ...git combo
function gitsync() {
    git add .
    git commit -m "$1"
    git push
}

# Path variables...
# ...Git repos
export GIT="$HOME/Documents/GitHub"
export SCRIPTS="$GIT/scripts"
export PATH="$SCRIPTS:$PATH"


# Display which shell is use
echo "Running Shell from $SHELL"
